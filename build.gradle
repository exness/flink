plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
    id 'org.jreleaser' version '1.19.0'
}

ext {
    javaVersion = '17'
    flinkVersion = '1.20.0'
    flinkKafkaVersion = '3.3.0-1.20'
    scalaBinaryVersion = '2.12'
    slf4jVersion = '2.0.3'
    log4jVersion = '2.17.1'
    lombokVersion = '1.18.34'
    assertjVersion = '3.23.1'
    junitJupiterVersion = '5.10.1'
    postgresJdbcVersion = '42.7.2'
    verticaJdbcVersion = '12.0.4-0'
    clickhouseJdbcVersion = '0.6.5'
    calciteVersion = '1.37.0'
    apicurioVersion = '2.5.8.Final'
}

allprojects {
    group = 'com.exness'
    version = project.hasProperty('projectVersion') ? project.property('projectVersion') : '0.1.0-SNAPSHOT'
    
    println "Setting project version to: ${version}"
    println "projectVersion property: ${project.hasProperty('projectVersion') ? project.property('projectVersion') : 'NOT SET'}"
    
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        withSourcesJar()
    }

    repositories {
        maven {
            url "https://packages.confluent.io/maven"
            allowInsecureProtocol true
        }
        maven {
            url 'https://jitpack.io'
        }
        mavenCentral()
    }

    checkstyle {
        configFile = rootProject.file('checkstyle/checkstyle.xml')
        toolVersion = '9.1'
        ignoreFailures = false
    }
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            groupId = group
            artifactId = project.name
            version = System.getenv("RELEASE_VERSION") ?: "local"

            from components.java
        }
    }
    
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    
    project {
        name = 'flink'
        version = '1.20.1'
        description = 'Various tools and improvements for Flink'
        website = 'https://github.com/exness/flink'
        authors = ['Anonymous <noreply@exness.com>']
        
        println "JReleaser project version: ${version}"
    }
    
    release {
        github {
            skipRelease = true
            skipTag = true
        }
    }
    
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}